import io.franzbecker.gradle.lombok.task.DelombokTask
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {
    ext {
        springBootVersion = '1.5.9.RELEASE'
        prometheusVersion = '0.0.26'
        jacksonVersion = '2.8.10'
        jettyVersion = '9.4.7.v20170914'
        lombokVersion = '1.16.16'
        vertxVersion = '3.5.0'
        amvVertxVersion = '0.0.1'
        guavaVersion = '23.4-jre'
        slf4jVersion = '1.7.25'
        mockitoVersion = '2.12.0'
        junitVersion = '4.12'
        hamcrestVersion = '1.3'
        metricsVersion = '3.2.2'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("nu.studer:gradle-credentials-plugin:1.0.3")
    }
}

plugins {
    id 'nebula.project' version '3.4.0'
    id 'nebula.info' version '3.6.0'
    id 'nebula.release' version '6.0.0'
    id 'com.github.spotbugs' version '1.6.1'
    id 'checkstyle'
    id 'com.jfrog.bintray' version '1.7.3'
    id 'io.franzbecker.gradle-lombok' version '1.8'
}

apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'nu.studer.credentials'

description = 'mesqueteltra'
group = 'com.github.theborakompanioni'

configurations.all {
    resolutionStrategy {
        // fail eagerly on version conflict (includes transitive dependencies)
        // e.g. multiple different versions of the same dependency (group and name are equal)
        failOnVersionConflict()
    }
}

contacts {
    'theborakompanioni+github@gmail.com' {
        moniker 'theborekompanioni'
        role 'owner'
        github 'theborakompanioni'
    }
}

/*
project.publishing.publications {
    // prevent maven publish from complaining about empty groupId
    nebula(MavenPublication) {
        groupId group
    }
}*/

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'nebula.project'
    apply plugin: 'nebula.info'
    apply plugin: 'io.franzbecker.gradle-lombok'
    //apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'com.jfrog.bintray'

    group = 'com.github.theborakompanioni'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // ---- prepare task graph - disable long running tasks on demand
    gradle.taskGraph.whenReady { graph ->
        def isMinimalBuild = project.hasProperty("minimal")

        def enableAdditionalLongRunningTasks = !isMinimalBuild

        if (!enableAdditionalLongRunningTasks) {
            spotbugsMain.enabled = false
            checkstyleMain.enabled = false
            javadoc.enabled = false
        }
    }

    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.7.1'

        compile "org.slf4j:slf4j-api:${slf4jVersion}"
        compile group: 'io.projectreactor', name: 'reactor-core', version: '3.1.5.RELEASE'
        compile group: 'com.google.guava', name: 'guava', version: '24.0-jre'

        compileOnly "org.projectlombok:lombok:${lombokVersion}"

        testCompile "junit:junit:${junitVersion}"
        testCompile "org.hamcrest:hamcrest-all:${hamcrestVersion}"
        testCompile 'pl.pragmatists:JUnitParams:1.1.0'
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
        testCompile 'org.apache.commons:commons-lang3:3.6'
        testCompile 'org.apache.commons:commons-text:1.2'

    }

    lombok {
        version = '1.16.16'
        sha256 = 'fbf682c7ff113e5187a9c4b4cbd7a8ad782abd8ccff91b9fa7289a4a6aa8c19a'
    }

    task delombok(type: DelombokTask, dependsOn: compileJava) {
        ext.outputDir = file("$buildDir/delombok")
        outputs.dir(outputDir)
        sourceSets.main.java.srcDirs.each {
            inputs.dir(it)
            args(it, '-d', outputDir)
        }
    }

    task delombokHelp(type: DelombokTask) {
        args '--help'
    }

    javadoc {
        dependsOn delombok
        source = delombok.outputDir
        failOnError = false
    }

    jar.doFirst {
        // other manifest attributes will be populated by nebula.info plugin
        // see https://github.com/nebula-plugins/gradle-info-plugin
        def vendor = 'theborakompanioni'
        manifest {
            attributes(
                    'Specification-Title': project.description,
                    'Specification-Version': project.version,
                    'Specification-Vendor': vendor,
                    'Implementation-Vendor': vendor
            )
        }
    }

    clean.doFirst {
        delete fileTree('./') {
            // delete temporary sqlite databases
            include '**/~*.db'
        }
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    task subprojectsDependencies(type: DependencyReportTask) {

    }

    /*checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        toolVersion = '6.7'
        ignoreFailures = true
        showViolations = false
        sourceSets = [sourceSets.main]
        reportsDir = rootProject.file("$buildDir/reports/checkstyle")
    }*/

    spotbugs {
        toolVersion = '3.1.2'
        effort = 'min'
        ignoreFailures = true
        sourceSets = [sourceSets.main]
    }

    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    tasks.withType(Test) {
        testLogging {
            // set options for log level LIFECYCLE: TestLogEvent.STARTED, TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.FAILED
            events TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.FAILED
            showExceptions true
            exceptionFormat TestExceptionFormat.FULL
            showCauses true
            showStackTraces true

            afterSuite { desc, result ->
                if (!desc.parent) { // will match the outermost suite
                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                    def startItem = '|  ', endItem = '  |'
                    def repeatLength = startItem.length() + output.length() + endItem.length()
                    println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
                }
            }
        }
    }

    if (project.hasProperty('releaseToBintray')) {
        if (rootProject.version.toString().contains('SNAPSHOT')) {
            throw new GradleException('-PreleaseToBintray param cannot be used with SNAPSHOT versions')
        }

        bintray {
            user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
            key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
            configurations = ['archives']
            pkg {
                repo = 'vertx-rx-springboot'
                name = project.name
                userOrg = user
                licenses = ['Apache-2.0']
                vcsUrl = 'https://github.com/theborakompanioni/mesqueteltra.git'
                labels = ['vertx', 'spring', 'moquette']
                publicDownloadNumbers = false
                version {
                    name = rootProject.version
                    desc = project.description
                    vcsTag = rootProject.version
                }
            }
        }
    }
}




